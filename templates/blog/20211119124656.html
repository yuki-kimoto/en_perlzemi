<h2>JavaScript Supported Web Scraping using Perl and Selenium</h2>

I explain JavaScript supported web scraping using Perl and <a href="https://metacpan.org/pod/Selenium::Remote::Driver">Selenium::Remote::Driver</a>. Selenium::Remote::Driver is a Perl module for Selenium. Selenium provides the APIs for JavaScript supported web scraping. 

<h3>Download and Install Perl</h3>

See the following articles about how to donwload and install Perl. Windows, Max, Linux/UNIX are supported.

<ul>
  <li><a href="/blog/20180820153471.html">Download and Install Perl</a></li>
</ul>

<h3>Download and Install Google Chrome</h3>

I explain the way to install web browsers.

<h4>Windows and Mac</h4>

You can download and install Google Chrome in the following page.

<ul>
 <li><a href="https://www.google.com/chrome/">Google Chrome - Download</a></li>
</ul>

<h4>Linux</h4>

In Linux you maybe install Google Chrome using package managers. I try the installing Google Chrome on the some Linux distributions.

<h5>Ubuntu</h5>

<ul>
 <li><a href="https://en.ubuntu.perlzemi.com/blog/20211117083934.html">Install Google Chrome on Ubuntu</a></li>
</ul>

<h5>CentOS</h5>

<ul>
 <li><a href="https://en.centos.perlzemi.com/blog/20210104090837.html">Install Google Chrome on CentOS</a></li>
</ul>

<h4>Check the version of Google Chrome</h4>

Check the version of Google Chrome.

<pre>
google-chrome --version
</pre>

An example of the output:

<pre>
Google Chrome 87.0.4280.88
</pre>

<h3>Download and Install ChromeDriver</h3>

ChromeDriver is a web driver. Web drivers is tools to control Web browsers by using programs.

<h5>Ubuntu</h5>

<ul>
 <li><a href="https://en.ubuntu.perlzemi.com/blog/20211116083934.html">Install ChromeDriver on Ubuntu</a></li>
</ul>

<h5>CentOS</h5>

<ul>
 <li><a href="https://en.centos.perlzemi.com/blog/20210103090837.html">Install ChromeDriver on CentOS</a></li>
</ul>

<h4>Check the version of ChromeDriver</h4>

Check the version of ChromeDriver.

<pre>
chromedriver --version
</pre>

An example of the output:

<pre>
ChromeDriver 87.0.4280.88 (89e2380a3e36c3464b5dd1302349b1382549290d-refs/branch-heads/4280@{#1761})
</pre>

It is good that the version of ChromeDriver is the same as Google Chrome to prevent problems.

<h3>Install Selenium::Remote::Driver</h3>

Let's install <a href="https://metacpan.org/pod/Selenium::Remote::Driver">Selenium::Remote::Driver</a> using <a href="/blog/20101027127859.html">cpanm</a> or <a href="/blog/20100504127089.html">cpan</a>. Selenium::Remote::Driver is a Perl module for Selenium.

<pre>
# cpanm
cpanm Selenium::Remote::Driver

# cpan
cpan Selenium::Remote::Driver
</pre>

<h3>Get a Web Page using Selenium</h3>

Get a Web Page using Selenium.

<h4>Load Selenium::Chrome</h4>

Let's load <b>Selenium::Chrome</b>. Selenium::Chrome is a sub class of Selenium::Remote::Driver for Google Chrome.

<pre>
use Selenium::Chrome;
</pre>

<h4>Create a Selenium::Chrome Object</h4>

Create a Selenium::Chrome object with headless options.

<pre>
my $driver = Selenium::Chrome->new(
  extra_capabilities => {
    'goog:chromeOptions' => {
      args => ['headless', 'disable-gpu', 'window-size=1920,1080', 'no-sandbox' ]
    }
  }
);
</pre>

These options is needed for headless execution. Headless execution means you get a page without the GUI of Google Chrome.

If there are no these options and you don't have GUI display, you will see the following error message.

<pre>
Could not create new session: unknown error: Chrome failed to start: exited abnormally.

(unknown error: DevToolsActivePort file doesn't exist) (The process started from chrome location /usr/bin/google-chrome is no longer running, so ChromeDriver is assuming that Chrome has crashed.) at access.pl line 79.
</pre>

<h4>Shutdown a Selenium::Chrome Object</h4>

If you finish the use of Selenium::Chrome, you need to shutdown it.

<pre>
$driver->shutdown_binary;
</pre>

<h4>Get a Web Page and the Title</h4>

You can get a web page using a <b>get</b> method.

<pre>
$driver->get('http://www.google.com');
</pre>

You can get the title using a <b>get_title</b> method.

<pre>
my $title = $driver->get_title;
</pre>

<b>Examples:</b>

This is an example to get a page and the title.

<pre>
use Selenium::Chrome;

# Create a Selenium::Chrome object
my $driver = Selenium::Chrome->new(
  extra_capabilities => {
    'goog:chromeOptions' => {
      args => ['headless', 'disable-gpu', 'window-size=1920,1080', 'no-sandbox' ]
    }
  }
);

# Get a page
$driver->get('http://www.google.com');

# Get the title
my $title = $driver->get_title;

print "$title\n";

# Shutdown
$driver->shutdown_binary;
</pre>

The output:

<pre>
Google
</pre>

<h4>Get the Screenshot</h4>

You can get the screenshot using <b>capture_screenshot</b> method.

<pre>
$driver->capture_screenshot("screenshot.png");
</pre>

If you want to save the screenshot to the program directory, you can use <a href="/blog/20100524127696.html">FindBin</a> module.

<pre>
use FindBin;

$driver->capture_screenshot("$FindBin::Bin/screenshot.png");
</pre>

<h3>Install Fonts</h3>

If you see character corruption when you get screenshots, the installing of fonts is needed.

I explain the way to install the fonts on some OSs.

<h4>Ubuntu</h4>

<ul>
  <li><a href="https://en.ubuntu.perlzemi.com/blog/20211124125149.html">Install Fonts on Ubuntu</a></li>
</ul>

<h4>CentOS</h4>

<ul>
  <li><a href="https://en.centos.perlzemi.com/blog/20211124121003.html">Install Fonts on CentOS</a></li>
</ul>

<h3>CSS Selector</h3>

If you are familiar with CSS Selector, you can make CSS Selector as the default finder.

<pre>
# Make CSS Selector as the default finder
$driver->default_finder('css');
</pre>

In this page, I use CSS selector to select elements of HTML.

<h3>Get Elements</h3>

I expain how to get elements using <a href="https://metacpan.org/pod/Selenium::Remote::Driver">Selenium::Remote::Driver</a>.

<h4>Get An Element</h4>

You can get an element using <b>find_element</b> method with CSS selector. The return value is <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>

<pre>
# Get an element using id CSS selector
my $element = $driver->find_element("#myid");

# Get an element using class CSS selector
my $element = $driver->find_element(".myclass");

# Get an element using attirbute CSS selector
my $element = $driver->find_element("[name=id]");
</pre>

Note that <b>find_element</b> method throws exception when the element is not found.

If you want to avoid exceptions, you need to use <a href="/blog/20100312126967.html">eval</a> block.

<pre>
my $element;
eval {
  $element = $driver->find_element("#myid");
};
</pre>

<h4>Get Multiple Elements</h4>

You can get multiple elements using <b>find_elements</b> method with CSS selector. The return value is an <a href="https://en.perlzemi.com/blog/20180214151861.html">array reference</a> which contains the list <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>

<pre>
# Get Multiple Elements
my $buttons = $driver->find_elements('button');
for my $button (@$buttons) {
  my $text = $button->get_text;
  if ($text =~ /Signin/) {
    $button->click;
  }
}
</pre>

Note that <b>find_elements</b> method throws exception when all the elements is not found.

If you want to avoid exceptions, you need to use <a href="/blog/20100312126967.html">eval</a> block.

<pre>
my $buttons;
eval {
  $buttons = $driver->find_elements('button');
};
</pre>

<h3>Text Fields, Password Fields, Text Area</h3>

I explain the manipulation of text fields, password fields, text areas.

<h4>Input Text</h4>

You can input text in text fields, password fields, text areas using <b>send_keys</b> method  of <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>.

<pre>
# Input text
$input_element->send_keys("perlclub");
</pre>

<b>send_keys</b> method supports Unicode. You need to write "use utf8;" and save the source code as UTF-8.

<pre>
# Input unicode text
use strict;
use warnings;
use utf8;

# ...

$input_element->send_keys("あいうえお");
</pre>

<h3>Checkbox and Radio Button</h3>

I explain how to check the chekking of checkboxes and radio buttons and set the chekking of checkboxes and radio buttons.

<h4>Check the Chekking of Checkboxes and Radio Buttons</h4>

You can check the checking of checkboxes or radio buttons using <b>is_selected</b> method of <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>.

<pre>
# Check the Chekking of Checkboxes or Radio Buttons
my $is_selected = $element->is_selected;
</pre>

<h4>Set the Chekking of Checkboxes or Radio Buttons</h4>

You can set the chekking of checkboxes or radio buttons <b>set_selected</b> method of <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>.

<pre>
# Set the Chekking of Checkboxes or Radio Buttons
$element->set_selected;
</pre>

You maybe see the following error "Other element would receive the click".

<pre>
Error while executing command: element click intercepted: element click intercepted: Element <input type="checkbox" class="myclass" value="true" id="myid"> is not clickable at point (1009, 210). Other element would receive the click: <label class="mylabel" for="myid">...</label>
  (Session info: headless chrome=87.0.4280.88) at /home/perlclub/perl5/perlbrew/perls/perl-5.20.3/lib/site_perl/5.20.3/Selenium/Remote/Driver.pm line 410.
 at /home/perlclub/perl5/perlbrew/perls/perl-5.20.3/lib/site_perl/5.20.3/Selenium/Remote/Driver.pm line 361.
</pre>

This is because label block receive the click in the logic of <b>set_selected</b> method.

You can write the following codes to click the label. The label block is a sibling of #myid

<pre>
my $checkbox = $driver->find_element("#myid");
unless ($checkbox->is_selected) {
  my $checkbox_label = $driver->find_element("#myid ~ label");
  $checkbox_label->set_selected;
}
</pre>

<h3>Click Buttons</h3>

You can click buttons using <b>click</b> method  of <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>.

<pre>
# Click Buttons
$button->click;
</pre>

<h3>Get Text</h3>

You can get text of an element using <b>get_text</b> method  of <a href="https://metacpan.org/pod/Selenium::Remote::WebElement">Selenium::Remote::WebElement</a>.

<pre>
# Get text
my $text = $element->get_text;
</pre>

(Uncompleted)
